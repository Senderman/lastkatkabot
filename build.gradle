plugins {
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "4.4.4"
    id("io.micronaut.aot") version "4.4.4"
}

group 'com.senderman'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    // telegram
    implementation('com.annimon:tgbots-module:8.0.0') {
        exclude group: 'org.glassfish.grizzly', module: 'grizzly-http-server'
        exclude group: 'org.glassfish.jersey.core', module: 'jersey-server'
        exclude group: 'org.telegram', module: 'telegrambots-webhook'
    }

    implementation("jakarta.annotation:jakarta.annotation-api")

    // serialization
    annotationProcessor("io.micronaut:micronaut-http-validation")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-jackson-databind")

    // monitoring
    annotationProcessor("io.micronaut.micrometer:micronaut-micrometer-annotation")
    implementation("io.micronaut:micronaut-management")
    implementation("io.micronaut.micrometer:micronaut-micrometer-core")
    implementation("io.micronaut.micrometer:micronaut-micrometer-registry-prometheus")

    // database
    annotationProcessor("io.micronaut.data:micronaut-data-processor")
    implementation("io.micronaut.data:micronaut-data-jdbc")
    implementation("io.micronaut.flyway:micronaut-flyway")
    implementation("io.micronaut.sql:micronaut-jdbc-hikari")
    runtimeOnly("org.flywaydb:flyway-database-postgresql")
    runtimeOnly("org.postgresql:postgresql")


    implementation("ch.qos.logback:logback-classic")
    implementation("io.micronaut:micronaut-retry")
}

application {
    mainClass.set("com.senderman.lastkatkabot.LastkatkaBot")
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime 'netty'
    processing {
        incremental(true)
        annotations("com.senderman.lastkatkabot.*")
    }
    aot {
        // Please review carefully the optimizations enabled below
        // Check https://micronaut-projects.github.io/micronaut-aot/latest/guide/ for more details
        optimizeServiceLoading = true
        convertYamlToJava = true
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = false
        optimizeNetty = true
        replaceLogbackXml = true
    }
}